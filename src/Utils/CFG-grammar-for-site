PROGRAM -> DECLARATIONLIST eof.
DECLARATIONLIST ->DECLARATION DECLARATIONLIST |.
DECLARATION -> TYPESPECIFIER id DECLARATION2.
DECLARATION2 -> ; | bopen num blose ; | ( PARAMS ) COMPOUNDSTMT.
TYPESPECIFIER -> int | void.
PARAMS -> int id PARAM2 PARAMLIST2 | void PARAMS2.
PARAMS2 -> id PARAM2 PARAMLIST2.
PARAMLIST -> PARAM PARAMLIST2.
PARAMLIST2 -> comma PARAM PARAMLIST2 |.
PARAM -> TYPESPECIFIER id PARAM2.
PARAM2 -> bopen bclose |.
COMPOUNDSTMT -> aopen DECLARATIONLIST STATEMENTLIST aclose.
STATEMENTLIST -> STATEMENT STATEMENTLIST | .
STATEMENT -> EXPRESSIONSTMT | COMPOUNDSTMT | SELECTIONSTMT | ITERATIONSTMT | RETURNSTMT | SWITCHSTMT.
EXPRESSIONSTMT -> EXPRESSION ; | continue ; | break ; | ;.
SELECTIONSTMT -> if ( EXPRESSION ) STATEMENT else STATEMENT.
ITERATIONSTMT -> while ( EXPRESSION ) STATEMENT.
RETURNSTMT -> return RETURNSTMT2.
RETURNSTMT2 -> ; | EXPRESSION ;.
SWITCHSTMT -> switch ( EXPRESSION ) aopen CASESTMTS DEFAULTSTMT aclose.
CASESTMTS -> CASESTMT CASESTMTS | .
CASESTMT -> case num : STATEMENTLIST.
DEFAULTSTMT -> default : STATEMENTLIST | .
EXPRESSION -> id EXPRESSION2 | + FACTOR TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2 | minus FACTOR TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2 | ( EXPRESSION ) TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2 | num TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2.
EXPRESSION2 -> ( ARGS ) TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2 | VAR2 EXPRESSION3.
EXPRESSION3 -> equal EXPRESSION | TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2.
VAR -> id VAR2.
VAR2 -> bopen EXPRESSION bclose |.
SIMPLEEXPRESSION -> ADDITIVEEXPRESSION SIMPLEEXPRESSION2.
SIMPLEEXPRESSION2 -> RELOP ADDITIVEEXPRESSION |.
RELOP -> less | equalequal.
ADDITIVEEXPRESSION -> TERM ADDITIVEEXPRESSION2.
ADDITIVEEXPRESSION2 -> ADDOP TERM ADDITIVEEXPRESSION2 |.
ADDOP -> + | minus.
TERM -> SIGNEDFACTOR TERM2.
TERM2 -> * SIGNEDFACTOR TERM2 |.
SIGNEDFACTOR -> FACTOR | + FACTOR | minus FACTOR.
FACTOR -> ( EXPRESSION ) | id FACTOR2 | num.
FACTOR2 -> ( ARGS ) | VAR2.
ARGS -> ARGLIST | .
ARGLIST -> EXPRESSION ARGLIST2.
ARGLIST2 -> comma EXPRESSION ARGLIST2 |.

/* best version */