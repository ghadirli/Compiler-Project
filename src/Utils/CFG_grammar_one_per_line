PROGRAM -> DECLARATIONLIST eof
DECLARATIONLIST ->DECLARATION DECLARATIONLIST
DECLARATIONLIST -> ε
DECLARATION -> TYPESPECIFIER id DECLARATION2
DECLARATION2 -> ;
DECLARATION2 -> [ num ] ;
DECLARATION2 -> ( PARAMS ) COMPOUNDSTMT
TYPESPECIFIER -> int
TYPESPECIFIER -> void
PARAMS -> int id PARAM2 PARAMLIST2
PARAMS -> void PARAMS2
PARAMS2 -> id PARAM2 PARAMLIST2
PARAMLIST -> PARAM PARAMLIST2
PARAMLIST2 -> , PARAM PARAMLIST2
PARAMLIST2 -> ε
PARAM -> TYPESPECIFIER id PARAM2
PARAM2 -> [ ]
PARAM2 -> ε
COMPOUNDSTMT -> { DECLARATIONLIST STATEMENTLIST }
STATEMENTLIST -> STATEMENT STATEMENTLIST
STATEMENTLIST -> ε
STATEMENT -> EXPRESSIONSTMT
STATEMENT -> COMPOUNDSTMT
STATEMENT -> SELECTIONSTMT
STATEMENT -> ITERATIONSTMT
STATEMENT -> RETURNSTMT
STATEMENT -> SWITCHSTMT
EXPRESSIONSTMT -> EXPRESSION ;
EXPRESSIONSTMT -> continue ;
EXPRESSIONSTMT -> break ;
EXPRESSIONSTMT -> ;
SELECTIONSTMT -> if ( EXPRESSION ) STATEMENT else STATEMENT
ITERATIONSTMT -> while ( EXPRESSION ) STATEMENT
RETURNSTMT -> return RETURNSTMT2
RETURNSTMT2 -> EXPRESSION ;
RETURNSTMT2 -> ;
SWITCHSTMT -> switch ( EXPRESSION ) { CASESTMTS DEFAULTSTMT }
CASESTMTS -> CASESTMT CASESTMTS
CASESTMTS -> ε
CASESTMT -> case num : STATEMENTLIST
DEFAULTSTMT -> default : STATEMENTLIST
DEFAULTSTMT -> ε
EXPRESSION -> id EXPRESSION2
EXPRESSION -> + FACTOR TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
EXPRESSION -> - FACTOR TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
EXPRESSION -> ( EXPRESSION ) TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
EXPRESSION -> num TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
EXPRESSION2 -> ( ARGS ) TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2 | VAR2 EXPRESSION3
EXPRESSION3 -> = EXPRESSION
EXPRESSION3 -> TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
VAR -> id VAR2
VAR2 -> [ EXPRESSION ]
VAR2 -> ε
SIMPLEEXPRESSION -> ADDITIVEEXPRESSION SIMPLEEXPRESSION2
SIMPLEEXPRESSION2 -> RELOP ADDITIVEEXPRESSION
SIMPLEEXPRESSION2 -> ε
RELOP -> less
RELOP -> ==
ADDITIVEEXPRESSION -> TERM ADDITIVEEXPRESSION2
ADDITIVEEXPRESSION2 -> ADDOP TERM ADDITIVEEXPRESSION2
ADDITIVEEXPRESSION2 -> ε
ADDOP -> +
ADDOP -> -
TERM -> SIGNEDFACTOR TERM2
TERM2 -> * SIGNEDFACTOR TERM2
TERM2 -> ε
SIGNEDFACTOR -> FACTOR
SIGNEDFACTOR -> + FACTOR
SIGNEDFACTOR -> - FACTOR
FACTOR -> ( EXPRESSION )
FACTOR -> id FACTOR2
FACTOR -> num
FACTOR2 -> ( ARGS )
FACTOR2 -> VAR2
ARGS -> ARGLIST
ARGS -> ε
ARGLIST -> EXPRESSION ARGLIST2
ARGLIST2 -> , EXPRESSION ARGLIST2
ARGLIST2 -> ε