PROGRAM -> DECLARATIONLIST eof.
DECLARATIONLIST ->DECLARATION DECLARATIONLIST |.
DECLARATION -> TYPESPECIFIER id DECLARATION2.
DECLARATION2 -> ; | [ num ] ; | ( PARAMS ) COMPOUNDSTMT.
TYPESPECIFIER -> int | void.
PARAMS -> int id PARAM2 PARAMLIST2 | void PARAMS2.
PARAMS2 -> id PARAM2 PARAMLIST2.
PARAMLIST -> PARAM PARAMLIST2.
PARAMLIST2 -> , PARAM PARAMLIST2 |.
PARAM -> TYPESPECIFIER id PARAM2.
PARAM2 -> [ ] |.
COMPOUNDSTMT -> { DECLARATIONLIST STATEMENTLIST }.
STATEMENTLIST -> STATEMENT STATEMENTLIST | .
STATEMENT -> EXPRESSIONSTMT | COMPOUNDSTMT | SELECTIONSTMT | ITERATIONSTMT | RETURNSTMT | SWITCHSTMT.
EXPRESSIONSTMT -> EXPRESSION ; | continue ; | break ; | ;.
SELECTIONSTMT -> if ( EXPRESSION ) STATEMENT else STATEMENT.
ITERATIONSTMT -> while ( EXPRESSION ) STATEMENT.
RETURNSTMT -> return RETURNSTMT2.
RETURNSTMT2 -> ; | EXPRESSION ;.
SWITCHSTMT -> switch ( EXPRESSION ) { CASESTMTS DEFAULTSTMT }.
CASESTMTS -> CASESTMT CASESTMTS | .
CASESTMT -> case num : STATEMENTLIST.
DEFAULTSTMT -> default : STATEMENTLIST | .
EXPRESSION -> id EXPRESSION2 | + FACTOR TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2 | - FACTOR TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2 | ( EXPRESSION ) TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2 | num TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2.
EXPRESSION2 -> ( ARGS ) TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2 | VAR2 EXPRESSION3.
EXPRESSION3 -> = EXPRESSION | TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2.
VAR -> id VAR2.
VAR2 -> [ EXPRESSION ] |.
SIMPLEEXPRESSION -> ADDITIVEEXPRESSION SIMPLEEXPRESSION2.
SIMPLEEXPRESSION2 -> RELOP ADDITIVEEXPRESSION |.
RELOP -> less | ==.
ADDITIVEEXPRESSION -> TERM ADDITIVEEXPRESSION2.
ADDITIVEEXPRESSION2 -> ADDOP TERM ADDITIVEEXPRESSION2 |.
ADDOP -> + | -.
TERM -> SIGNEDFACTOR TERM2.
TERM2 -> * SIGNEDFACTOR TERM2 |.
SIGNEDFACTOR -> FACTOR | + FACTOR | - FACTOR.
FACTOR -> ( EXPRESSION ) | id FACTOR2 | num.
FACTOR2 -> ( ARGS ) | VAR2.
ARGS -> ARGLIST | .
ARGLIST -> EXPRESSION ARGLIST2.
ARGLIST2 -> , EXPRESSION ARGLIST2 |.
Îµ
/* best version */