PROGRAM -> DECLARATIONLIST eof
DECLARATIONLIST -> DECLARATION DECLARATIONLIST
DECLARATIONLIST -> ε
DECLARATION -> void id DECLARATION2
DECLARATION -> int id DECLARATION2
DECLARATION2 -> VARDECLARATION2
DECLARATION2 -> ( PARAMS ) COMPOUNDSTMT
VARDECLARATION2 -> ;
VARDECLARATION2 -> [ num ] ;
PARAMS -> int id PARAM2 PARAMLIST2
PARAMS -> void PARAMS2
PARAMS2 -> id PARAM2 PARAMLIST2
PARAMS2 -> ε
PARAMLIST2 -> , PARAMLIST3
PARAMLIST2 -> ε
PARAMLIST3 -> int id PARAM2 PARAMLIST2
PARAMLIST3 -> void id PARAM2 PARAMLIST2
PARAM2 -> [ ]
PARAM2 -> ε
COMPOUNDSTMT -> { DECLARATIONLIST STATEMENTLIST }
STATEMENTLIST -> STATEMENT STATEMENTLIST
STATEMENTLIST -> ε
STATEMENT -> EXPRESSIONSTMT
STATEMENT -> COMPOUNDSTMT
STATEMENT -> SELECTIONSTMT
STATEMENT -> ITERATIONSTMT
STATEMENT -> RETURNSTMT
STATEMENT -> SWITCHSTMT
EXPRESSIONSTMT -> EXPRESSION ;
EXPRESSIONSTMT -> continue ;
EXPRESSIONSTMT -> break ;
EXPRESSIONSTMT -> ;
SELECTIONSTMT -> if ( EXPRESSION ) STATEMENT else STATEMENT
ITERATIONSTMT -> while ( EXPRESSION ) STATEMENT
RETURNSTMT -> return RETURNSTMT2
RETURNSTMT2 -> ;
RETURNSTMT2 -> EXPRESSION ;
SWITCHSTMT -> switch ( EXPRESSION ) { CASESTMTS DEFAULTSTMT }
CASESTMTS -> CASESTMT CASESTMTS
CASESTMTS -> ε
CASESTMT -> case num : STATEMENTLIST
DEFAULTSTMT -> default : STATEMENTLIST
DEFAULTSTMT -> ε
EXPRESSION -> id EXPRESSION2
EXPRESSION -> num TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
EXPRESSION -> ( EXPRESSION ) TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
EXPRESSION -> + FACTOR TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
EXPRESSION -> - FACTOR TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
EXPRESSION2 -> VAR2 EXPRESSION3
EXPRESSION2 -> ( ARGS ) TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
EXPRESSION3 -> = EXPRESSION
EXPRESSION3 -> TERM2 ADDITIVEEXPRESSION2 SIMPLEEXPRESSION2
VAR2 -> [ EXPRESSION ]
VAR2 -> ε
SIMPLEEXPRESSION2 -> RELOP SIGNEDFACTOR TERM2 ADDITIVEEXPRESSION2
SIMPLEEXPRESSION2 -> ε
RELOP -> <
RELOP -> ==
ADDITIVEEXPRESSION2 -> ADDOP SIGNEDFACTOR TERM2 ADDITIVEEXPRESSION2
ADDITIVEEXPRESSION2 -> ε
ADDOP -> +
ADDOP -> -
TERM2 -> * SIGNEDFACTOR TERM2
TERM2 -> ε
SIGNEDFACTOR -> FACTOR
SIGNEDFACTOR -> + FACTOR
SIGNEDFACTOR -> - FACTOR
FACTOR -> ( EXPRESSION )
FACTOR -> id FACTOR2
FACTOR -> num
FACTOR2 -> VAR2
FACTOR2 -> ( ARGS )
ARGS -> ARGLIST
ARGS -> ε
ARGLIST -> EXPRESSION ARGLIST2
ARGLIST2 -> , EXPRESSION ARGLIST2
ARGLIST2 -> ε